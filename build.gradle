buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven'

group = GROUP
version = VERSION_NAME

ext {
    targetSdk = 27
    compileSdk = 27
}

android {
    compileSdkVersion rootProject.ext.compileSdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion rootProject.ext.targetSdk
        testApplicationId "com.mixpanel.android.mpmetrics"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        consumerProguardFiles 'proguard.txt'

        buildConfigField "String", "MIXPANEL_VERSION", "\"${version}\""

        lintOptions {
            abortOnError true
            warningsAsErrors false
            disable "UnusedResources"
            textOutput "stdout"
            textReport true
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

// Note- *all* dependencies are marked as optional in our final
// pom! If you need a transitive dependency in the library, you'll
// have to change the bit in uploadArchives that marks all dependencies as optional.
dependencies {
    compile 'com.android.support:support-annotations:27.1.1'
    compile 'com.android.support:support-v4:27.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}